import java.nio.file.Files

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.yagatalk'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.flywaydb:flyway-core'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "org.testcontainers:postgresql:1.18.3"
	testImplementation "org.testcontainers:junit-jupiter:1.18.3"
	testImplementation "com.github.tomakehurst:wiremock-standalone:3.0.0-beta-8"
}

tasks.register('copyWebComponent', Copy) { task ->
	into "${sourceSets.main.output.resourcesDir}/static"

	def webComponentBuildPath = "$rootDir/web-component/build"
	from("$webComponentBuildPath/js") {
		include("main.js")
		rename("main.js", "web-component.js")
		into("js")
	}

	from("$webComponentBuildPath/css") {
		include("main.css")
		rename("main.css", "web-component.css")
		into("css")
	}

	from ("$webComponentBuildPath/images") {
		into("images")
	}
}

tasks.register('copyAdminWebComponent', Copy) { task ->
	into "${sourceSets.main.output.resourcesDir}/static"

	def webComponentBuildPath = "$rootDir/admin-web-component/build"
	from("$webComponentBuildPath/js") {
		include("main.js")
		rename("main.js", "admin-web-component.js")
		into("js")
	}

	from("$webComponentBuildPath/css") {
		include("main.css")
		rename("main.css", "admin-web-component.css")
		into("css")
	}

	from ("$webComponentBuildPath/images") {
		into("images")
	}
}

classes {
	dependsOn(copyWebComponent)
	dependsOn(copyAdminWebComponent)
}

tasks.named('test') {
	useJUnitPlatform()
}
